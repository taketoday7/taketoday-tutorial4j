plugins {
    id 'java'
    id 'jacoco'
}

ext {
    junitVersion = '5.8.2'
    lombokVersion = '1.18.22'
}

group 'cn.tuyucheng.taketoday'
version '1.0.0'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

test {
    useJUnitPlatform()

    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "cn/tuyucheng/taketoday/**/ExcludedPOJO.class",
                    "cn/tuyucheng/taketoday/**/*DTO.*",
                    "**/config/*"
            ])
        }))
    }
}

jacoco {
    toolVersion = "0.8.6"
}